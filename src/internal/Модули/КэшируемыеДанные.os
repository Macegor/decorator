#Область ОписаниеПеременных

Перем ТекстШаблонаОбъектаДекоратора;       // Шаблон объекта дкоратора
Перем ТекстШаблонаМетодаДекоратора;        // Шаблон декорируемого метода
Перем ТекстШаблонаПользовательскогоМетода; // Шаблон добавляемого метода
Перем ТекстШаблонаТелоМетодаДекоратора;    // Шаблон тела декорируемого метода

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Текст шаблона с содержимым сценария декоратора
//
//  Возвращаемое значение:
//   Строка - Текст сценария
//
Функция ТекстШаблонаОбъектаДекоратора() Экспорт
	Если ТекстШаблонаОбъектаДекоратора = Неопределено Тогда
		ТекстШаблонаОбъектаДекоратора = ПрочитатьТекстШаблона("ШаблонДекоратора.os");
	КонецЕсли;
	
	Возврат ТекстШаблонаОбъектаДекоратора;
КонецФункции

Функция ТекстШаблонаМетодаДекоратора() Экспорт

	Если ТекстШаблонаМетодаДекоратора = Неопределено Тогда

		ТекстШаблона = ТекстШаблонаПользовательскогоМетода();

		ТекстШаблона = СтрЗаменить(ТекстШаблона, "// {Декоратор_ПередВызовомМетода}", "");
		ТекстШаблона = СтрЗаменить(ТекстШаблона, "// {Декоратор_ПослеВызоваМетода}", "");

		ТекстШаблонаМетодаДекоратора = СтрЗаменить(
			ТекстШаблона,
			"// {Декоратор_ТекстМетода}",
			ТекстШаблонаТелоМетодаДекоратора()
		);

	КонецЕсли;
	
	Возврат ТекстШаблонаМетодаДекоратора;
	
КонецФункции

Функция ТекстШаблонаПользовательскогоМетода() Экспорт
	Если ТекстШаблонаПользовательскогоМетода = Неопределено Тогда
		ТекстШаблонаПользовательскогоМетода = ПрочитатьТекстШаблона("ШаблонМетода.os_template");
	КонецЕсли;
	
	Возврат ТекстШаблонаПользовательскогоМетода;
КонецФункции

Функция ТекстШаблонаТелоМетодаДекоратора() Экспорт
	Если ТекстШаблонаТелоМетодаДекоратора = Неопределено Тогда
		ТекстШаблонаТелоМетодаДекоратора = ПрочитатьТекстШаблона("ШаблонТелоМетодаДекоратора.os_template");
	КонецЕсли;
	
	Возврат ТекстШаблонаТелоМетодаДекоратора;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПрочитатьТекстШаблона(ИмяШаблона)
	КаталогТекущегоСценария = ТекущийСценарий().Каталог;
	ПутьКШаблонуДекоратора = ОбъединитьПути(
		КаталогТекущегоСценария,
		"..",
		"Классы",
		ИмяШаблона
	);
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ПутьКШаблонуДекоратора, КодировкаТекста.UTF8NoBom);
	ТекстШаблонаДекоратора = ТекстовыйДокумент.ПолучитьТекст();
	ТекстовыйДокумент = Неопределено;
	
	Возврат ТекстШаблонаДекоратора;
КонецФункции

#КонецОбласти
